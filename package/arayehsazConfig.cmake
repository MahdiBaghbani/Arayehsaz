# CMake version.
cmake_minimum_required(VERSION 3.19 FATAL_ERROR)

set(arayehsaz_known_comps static shared)
set(arayehsaz_comp_static NO)
set(arayehsaz_comp_shared NO)
foreach (arayehsaz_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (arayehsaz_comp IN_LIST arayehsaz_known_comps)
        set(arayehsaz_comp_${arayehsaz_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
                "arayehsaz does not recognize component `${arayehsaz_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (arayehsaz_comp_static AND arayehsaz_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "arayehsaz `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(arayehsaz_static_targets "${CMAKE_CURRENT_LIST_DIR}/arayehsaz-static-targets.cmake")
set(arayehsaz_shared_targets "${CMAKE_CURRENT_LIST_DIR}/arayehsaz-shared-targets.cmake")

macro(arayehsaz_load_targets type)
    if (NOT EXISTS "${arayehsaz_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
                "arayehsaz `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${arayehsaz_${type}_targets}")
endmacro()

if (arayehsaz_comp_static)
    arayehsaz_load_targets(static)
elseif (arayehsaz_comp_shared)
    arayehsaz_load_targets(shared)
elseif (DEFINED ARAYEHSAZ_SHARED_LIBS AND ARAYEHSAZ_SHARED_LIBS)
    arayehsaz_load_targets(shared)
elseif (DEFINED ARAYEHSAZ_SHARED_LIBS AND NOT ARAYEHSAZ_SHARED_LIBS)
    arayehsaz_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${arayehsaz_shared_targets}")
        arayehsaz_load_targets(shared)
    else ()
        arayehsaz_load_targets(static)
    endif ()
else ()
    if (EXISTS "${arayehsaz_static_targets}")
        arayehsaz_load_targets(static)
    else ()
        arayehsaz_load_targets(shared)
    endif ()
endif ()
